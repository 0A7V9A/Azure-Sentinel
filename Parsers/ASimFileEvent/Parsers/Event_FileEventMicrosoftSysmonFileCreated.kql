// For reference, see: https://github.com/Azure/Azure-Sentinel/blob/master/Parsers/ASimFileEvent/ProductParsers/FileEventMicrosoftSysmonFileCreated.yaml
source
| where is_sentinel_workspace(TenantId)
| where Source == "Microsoft-Windows-Sysmon"
| where EventID == '11'
| parse EventData with '<DataItem type="System.XmlData" time="'Time:datetime
'" sourceHealthServiceId="'sourceHealthServiceId
'"><EventData xmlns="http://schemas.microsoft.com/win/2004/08/events/event"><Data Name="RuleName">'RuleName:string
'</Data><Data Name="UtcTime">'UtcTime:datetime'</Data><Data Name="ProcessGuid">{'ProcessGuid:string
'}</Data><Data Name="ProcessId">'ProcessId:string
'</Data><Data Name="Image">'Image:string /// Image is the full path 
'</Data><Data Name="TargetFilename">'TargetFilename:string //// Full Path
//'</Data><Data Name="CreationUtcTime">'CreationUtcTime:datetime* // hzahav important - added </Data> suffix to pattern matching can pass validation
'</Data><Data Name="CreationUtcTime">'CreationUtcTime:datetime'</Data>'*
| parse EventData with *'<Data Name="User">'ActorUsername'</Data>'*  // parsing will work only for newer versions of sysmon -> for older will remain empty field
| extend
    EventType='FileCreated'
    , EventProduct='Sysmon'
    , EventSchemaVersion = '0.1.0'
    , EventResult='Success'
    , EventCount=int(1)
    , EventStartTime = TimeGenerated
    , EventOriginalType=tostring(EventID)
    , EventEndTime = TimeGenerated
    , DvcOs='Windows'
    , TargetFileName_wo_Path=tostring(split(TargetFilename,'\\')[-1])
| project-rename
    DvcHostname = Computer
    , ActingProcessName = Image
    , ActingProcessId = ProcessId
    , ActingProcessGuid = ProcessGuid
    , TargetFileCreationTime=CreationUtcTime
    , EventMessage=RenderedDescription
    , TargetFilePath=TargetFilename
    ,TargetFileName=TargetFileName_wo_Path
| extend
        ActorUserType = iff(isnotempty(ActorUsername),'Windows', '')
        // aliases
        ,Process = ActingProcessName
        , Dvc = DvcHostname
        , FilePath = TargetFilePath
        , User = ActorUsername
| project-away EventData, sourceHealthServiceId, ParameterXml