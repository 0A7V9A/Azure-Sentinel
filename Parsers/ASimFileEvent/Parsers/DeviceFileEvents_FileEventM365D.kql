// For reference, see: https://github.com/Azure/Azure-Sentinel/blob/master/Parsers/ASimFileEvent/ProductParsers/FileEventM365D.yaml
let M365DFileEvents=(){
source
| where is_sentinel_workspace(TenantId)
| project-rename
        SrcIpAddr =RequestSourceIP
    , DvcId=DeviceId
    , TargetFileMD5=MD5
    , TargetFileSHA1 =SHA1
    , TargetFileSHA256 =SHA256
    , NetworkApplicationProtocol=RequestProtocol
    , ActingProcessCommandLine=InitiatingProcessCommandLine
    , ActingProcessName =InitiatingProcessFolderPath // Unlike FileName ActingProcessName includes full path  // Previously: ActingProcessFileName=initiatingProcessFileName
    , ActingProcessMD5 =InitiatingProcessMD5
    , ActingProcessSHA1 =InitiatingProcessSHA1
    , ActingProcessSHA256 =InitiatingProcessSHA256
    , ActingProcessParentFileName = InitiatingProcessParentFileName
    , ActingProcessCreationTime =InitiatingProcessCreationTime
    , ActingProcessParentCreationTime =InitiatingProcessParentCreationTime
| extend
        EventCount=int(1)
    , ActingProcessId = tostring(InitiatingProcessId)
    , EventStartTime= TimeGenerated 
    , EventEndTime= TimeGenerated
    , EventType=ActionType // [FileCreated, FileDeleted, FileModified, FileRenamed] 
    , EventResult= 'Success'
    , EventProduct='M365 Defender for Endpoint'
    , EventVendor='Microsoft'
    , EventSchemaVersion='0.1.0' 
    // ------------------
    , ActorUsername=case(isnotnull(InitiatingProcessAccountUpn), InitiatingProcessAccountUpn, strcat(InitiatingProcessAccountDomain,'\\', InitiatingProcessAccountName))
    , ActorUsernameType=iff(isempty(InitiatingProcessAccountUpn),'Windows','Upn')
    , ActorUserId=case(isnotnull(InitiatingProcessAccountSid), InitiatingProcessAccountSid, InitiatingProcessAccountObjectId)
    , ActorUserIdType=iff(isempty(InitiatingProcessAccountSid),'AADId','Sid') 
    , DvcHostname =DeviceName
    , DvcHostnameType=iff(DeviceName contains '.','Fqdn','Hostname')
    , TargetFilePath= iff(ActionType=='FileDeleted', strcat(FolderPath, '\\', FileName), FolderPath)
    // ------------------
    , TargetFilePathType='Windows Local'
    , SrcFileName=case(isnotnull(PreviousFileName),PreviousFileName,tostring(split(trim_end(@'/',tostring(split (FileOriginUrl,'?')[0] )),'/')[-1]))
    , SrcFilePath=iff(isempty(PreviousFileName), trim_end(@'/',tostring(split (FileOriginUrl,'?')[0] )),strcat(PreviousFolderPath,'\\',PreviousFileName)) 
    //  ****** Aliases
    | extend 
    User=ActorUsername
    , Dvc=DvcHostname
    , Hash=case(isnotnull(ActingProcessSHA256), ActingProcessSHA256, isnotnull(ActingProcessSHA1), ActingProcessSHA1, ActingProcessMD5)
    , FilePath=TargetFilePath
    };M365DFileEvents()