// For reference, see: https://github.com/Azure/Azure-Sentinel/blob/master/Parsers/ASimFileEvent/ProductParsers/FileEventMicrosoftSharePoint.yaml
let usertypes = parsejson('{ "Admin": "Admin", "DcAdmin": "Admin", "System": "System", "Application": "Application", "ServicePrincipal": "Service Principal", "CustomPolicy": "Other", "SystemPolicy": "Other", "Reserved": "Other" }');
let SharepointFile=(){
source
| where is_sentinel_workspace(TenantId)
| where OfficeWorkload in ('SharePoint', 'OneDrive')
        and ItemType in ('File', 'Folder', 'DocumentLibrary', 'Page')
| extend ActorUserType = usertypes[UserType]
| extend
        EventCount=int(1)
    , EventStartTime= TimeGenerated 
    , EventEndTime= TimeGenerated
    , EventType=Operation
    , EventResult=case(ResultStatus =='Succeeded', 'Success'
                        , ResultStatus == 'Failed', 'Failure'
                        , 'Partial')
    , EventProduct='SharePoint 365'
    , EventVendor='Microsoft'
    , EventSchemaVersion='0.1.0' 
    , ActorUsernameType='upn'
    , ActorUserIdType='Puid' //(PUID) for events performed by users in SharePoint, OneDrive
    /// If operation is copied/moved than SourceFile us the "Previous file". (what about FileRenamed?)
    , TargetFilePath =iff(Operation in ('FileCopied', 'FileMoved')
                            , DestinationFileName
                            , strcat(Site_Url, '/', SourceRelativeUrl,'/',SourceFileName)) 
    ,  TargetFilename=iff(Operation in ('FileCopied', 'FileMoved'), DestinationFileName, SourceFileName) 
    // , SensitivityLabel:string // ? 
    , SrcFileName=iff(Operation in ('FileCopied', 'FileMoved'), SourceFileName,'')
    , SrcFilePath=iff(Operation in ('FileCopied', 'FileMoved'), strcat(Site_Url, '/', SourceRelativeUrl),'')
    ////  Not in scheam??  , TargetAppName='SharePoint'
    | project-rename ActorOriginalUserType =UserType
        , ActorUsername=UserId 
        , ActorUserId=UserKey
        , HttpUserAgent=UserAgent
        , SrcIpAddr =ClientIP
    // Aliases
    | extend 
    User=ActorUsername
    , FilePath=TargetFilename
};
SharepointFile()