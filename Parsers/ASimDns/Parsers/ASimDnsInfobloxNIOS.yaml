Id: 678b6e84-0308-461c-ae43-6bb287a9d970
Parser:
  Title: DNS activity ASIM parser for Infoblox NIOS
  Version: '0.5'
  LastUpdated: June 26 2022
Product:
  Name: Infoblox NIOS
Normalization:
  Schema: Dns
  Version: '0.1.3'
References:
- Title: ASIM DNS Schema
  Link: https://aka.ms/ASimDnsDoc
- Title: ASIM
  Link: https://aka.ms/AboutASIM
Description: |
  This ASIM parser supports normalizing Infoblox NIOS DNS logs to the ASIM Dns normalized schema.
ParserName: ASimDnsInfobloxNIOS
EquivalentBuiltInParser: _ASim_Dns_InfobloxNIOS
ParserParams:
  - Name: disabled
    Type: bool
    Default: false
ParserQuery: |
 let response = (disabled: boolean=false) {
    Syslog
    | where not(disabled)
    | where ProcessName == "named" and SyslogMessage has_all ("client", "query:", "response:")
    | parse SyslogMessage with *
        "client " SrcIpAddr: string
        "#" SrcPortNumber: string
        " " NetworkProtocol: string
        ": query: " DnsQuery: string
        " " DnsQueryClassName: string
        " " DnsQueryTypeName: string
        " response: " DnsResponseCodeName: string
        " " DnsFlags: string
    | extend DNSResourceRecordIndex= indexof(DnsFlags, " ")
    | extend DNSResourceRecord =iif(DNSResourceRecordIndex != "-1", substring(DnsFlags, DNSResourceRecordIndex), "")
    | extend DnsFlags =iif(DNSResourceRecordIndex != "-1", substring(DnsFlags, 0, DNSResourceRecordIndex), DnsFlags)
    | extend EventSubType = "response"
    | project-away DNSResourceRecordIndex,SyslogMessage, ProcessName, ProcessID, Facility, SeverityLevel, HostName
  };
  let request = (disabled: boolean=false) {
    Syslog 
    | where not(disabled)
    | where ProcessName == "named" and SyslogMessage has_all ("client", "query:") and SyslogMessage !has "response:"
    | parse SyslogMessage with *
        "client " restData: string
    | extend restData = iif(restData startswith "@", (substring(restData, indexof(restData, " "))), restData)
    | extend restData = replace_string(restData,"\\ ","@@@")
    | parse restData with 
        SrcIpAddr: string
        "#" SrcPortNumber: string
        " " *
        "query: " DnsQuery: string
        " " DnsQueryClassName: string
        " " DnsQueryTypeName: string
        " " DnsFlags: string
    | extend DnsQuery = replace_string (DnsQuery, '@@@', ' ')
    | extend ServerIPAddressIndex= indexof(DnsFlags, " ")
    | extend ServerIPAddress = iif(ServerIPAddressIndex != "-1", substring(DnsFlags, ServerIPAddressIndex), "")
    | extend ServerIPAddress = replace_regex(ServerIPAddress,@"[()]","")
    | extend DnsFlags =iif(ServerIPAddressIndex != "-1", substring(DnsFlags, 0, ServerIPAddressIndex), DnsFlags)
    | extend SrcPortNumber = replace_regex(SrcPortNumber,@"[^\d]","")
    | extend 
        EventSubType = "request",
        DnsResponseCodeName = "NA"
    | project-away ServerIPAddressIndex,SyslogMessage,restData, ProcessName, ProcessID, Facility, SeverityLevel, HostName
  };
  let parser = (disabled:boolean=false) {
    union response (disabled), request (disabled)
    | extend
        EventCount=int(1),
        EventStartTime=todatetime(TimeGenerated),
        EventEndTime=todatetime(TimeGenerated),
        EventProduct="NIOS",
        EventVendor="Infoblox",
        EventSchema="Dns",
        EventSchemaVersion="0.1.3",
        EventType="Query", 
        EventResult=iff(EventSubType=="request" or DnsResponseCodeName=="NOERROR","Success","Failure"),
        DvcIpAddr=iff (HostIP == "Unknown IP", "", HostIP)
    // -- Aliases
    | invoke _ASIM_ResolveDvcFQDN ("Computer")
    | project-away Computer
    | extend
        Dvc=DvcHostname,
        Domain=DnsQuery,
        IpAddr=SrcIpAddr,
        Src=SrcIpAddr,
        EventResultDetails = DnsResponseCodeName
    // -- Backward Compatibility
    | extend
        Query=DnsQuery,
        QueryTypeName=DnsQueryTypeName,
        ResponseCodeName=DnsResponseCodeName,
        QueryClassName=DnsQueryClassName,
        Flags=DnsFlags
  };
  parser (disabled)