Parser:
  Title: ASIM Azure Defender for IoT Process Events Parser
  Version: '0.1.0'
  LastUpdated: Aug 4, 2021
Product:
  Name: Azure Defender for IoT
Normalization:
  Schema: ProcessEvent
  Version: '0.1.0'
References:
- Title: ASIM Process Schema
  Link: https://aka.ms/AzSentinelProcessEventsDoc
- Title: ASIM
  Link: https:/aka.ms/AzSentinelNormalization
Description: ASIM Azure Defender for IoT Process Events Parser
ParserName: vimProcessEventAD4IoT
ParserQuery: |
              let ProcessEvents_AD4IoT=()
              {
                SecurityIoTRawEvent 
                | where RawEventName == "Process" // OK+W
                | extend
                  EventDetails = todynamic(EventDetails) // OK
                  //
                | extend
                  // Event
                  EventOriginalUid = EventDetails.EventId, // OK+W
                  EventCount = toint(EventDetails.HitCount), // OK+W
                  EventProduct = 'Azure Defender for IoT', // OK+W
                  EventVendor = 'Microsoft', // OK+W
                  EventSchemaVersion = '0.1.0', // OK+W
                  EventStartTime = todatetime(EventDetails.TimestampUTC), // OK+W
                  EventEndTime = todatetime(TimeGenerated), // OK+W
                  EventType = iff (EventDetails.EventType == 'EXIT', 'ProcessTerminate', 'ProcessCreated'), // OK+W
                  EventSubType = EventDetails.EventType, // OK+W - Add to docs, FORK, EXEC
                  EventResult = 'Success', // OK+W
                  //
                  // Device
                  DvcHostname = EventDetails.DeviceId, // OK+W
                  //
                  // Users
                  //
                  // Process
                  TargetProcessId = tostring(EventDetails.ProcessId), // OK+W
                  TargetProcessCommandLine = coalesce (EventDetails.Commandline, EventDetails.Executable), // OK+W
                  TargetProcessName = coalesce (EventDetails.Executable, split(EventDetails.Commandline," ")[0]) // OK
                  //
                | extend
                  DvcOs = iif (TargetProcessName matches regex '^[A-Z]\\:', "Windows", "Linux") // OK+W
                | extend
                  TargetUserNameType = iif (DvcOs == "Windows", "Windows", "Simple"), // OK+W
                  TargetUsername = iff (DvcOs == "Windows", EventDetails.UserName, ""), // OK+W
                  ActingProcessId = iff (DvcOs == "Windows", tostring(EventDetails.ParentProcessId), "") // OK+W
               | project-rename
                  // EventProductVersion = AgentVersion, // Not available in Windows
                  _ResourceId = AssociatedResourceId, // OK+W
                  _SubscriptionId = AzureSubscriptionId // OK+W
                  //
                  // -- aliases
                | extend 
                  User = TargetUsername, // OK+W
                  CommandLine = TargetProcessCommandLine, // OK+W
                  Process = TargetProcessName, // OK+W
                  Dvc = DvcHostname // OK+W
               };
               ProcessEvents_AD4IoT