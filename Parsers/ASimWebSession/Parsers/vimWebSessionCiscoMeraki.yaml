Parser:
  Title: Web Session ASIM parser for Cisco Meraki
  Version: '0.1'
  LastUpdated: Jun 12 2023
Product:
  Name: Cisco Meraki
Normalization:
  Schema: WebSession
  Version: '0.2.6'
References:
- Title: ASIM Web Session Schema
  Link: https://aka.ms/ASimWebSessionDoc
- Title: ASIM
  Link: https:/aka.ms/AboutASIM
Description: |
  This ASIM parser supports normalizing Cisco Meraki logs to the ASIM Web Session normalized schema.
ParserName: vimWebSessionCiscoMeraki
ParserParams:
  - Name: starttime
    Type: datetime
    Default: datetime(null)
  - Name: endtime
    Type: datetime
    Default: datetime(null)
  - Name: srcipaddr_has_any_prefix
    Type: dynamic
    Default: dynamic([])
  - Name: dstipaddr_has_any_prefix
    Type: dynamic
    Default: dynamic([])
  - Name: ipaddr_has_any_prefix
    Type: dynamic
    Default: dynamic([])
  - Name: url_has_any
    Type: dynamic
    Default: dynamic([])
  - Name: httpuseragent_has_any
    Type: dynamic 
    Default: dynamic([])
  - Name: eventresult
    Type: string
    Default: '*'
  - Name: disabled
    Type: bool
    Default: false
ParserQuery: |
  let ActionLookup = datatable (Action: string, DvcAction: string)  [
    'allow', 'Allow',
    'log', 'Allow',
    'accept', 'Allow',
    'block', 'Deny',
    'deny', 'Deny',
    'quarantine', 'Deny'
  ];
  let parser=(
    starttime: datetime=datetime(null), 
    endtime: datetime=datetime(null),
    srcipaddr_has_any_prefix: dynamic=dynamic([]),
    dstipaddr_has_any_prefix: dynamic=dynamic([]),
    ipaddr_has_any_prefix: dynamic=dynamic([]), 
    url_has_any: dynamic=dynamic([]),
    httpuseragent_has_any: dynamic=dynamic([]),
    eventresult: string='*',
    disabled: bool=false
    ) {
    let src_or_any=set_union(srcipaddr_has_any_prefix, ipaddr_has_any_prefix); 
    let dst_or_any=set_union(dstipaddr_has_any_prefix, ipaddr_has_any_prefix);
    union isfuzzy=true
        (
        meraki_CL
        | project-rename LogMessage =  Message
        ),
        (
        Syslog
        | project-rename LogMessage =  SyslogMessage
        )
    | where not(disabled) and (isnull(starttime) or TimeGenerated >= starttime)
        and (isnull(endtime) or TimeGenerated <= endtime)
    | where (LogMessage has "urls" or (LogMessage has "security_event" and LogMessage has "security_filtering_file_scanned"))
    | extend Parser = extract_all(@"(\d+.\d+)\s([\w\-\_]+)\s([\w\-\_]+)\s([\S\s]+)$", dynamic([1, 2, 3, 4]), LogMessage)[0]
    | extend
        Epoch = tostring(Parser[0]),
        Device =tostring(Parser[1]),
        Substring = tostring(Parser[3]),
        EventCount=int(1),
        EventProduct="Meraki",
        EventVendor="Cisco",
        EventSchema="WebSession",
        EventSchemaVersion="0.2.6"
    | extend EpochTimestamp = split(Epoch, ".")
    | extend
        EventStartTime = unixtime_seconds_todatetime(tolong(EpochTimestamp[0])),
        EventEndTime=TimeGenerated,
        Disposition=tostring(extract(@"disposition=(\S+)\s", 1, Substring)),
        Action=case(LogMessage has "security_event", tostring(extract(@"action=(\S+)", 1, Substring)), ""),
        Url = case(
          LogMessage has "urls",
          extract(@"request: (\w+)\s(\S+)", 2, Substring),
          LogMessage has "security_event",
          tostring(extract(@"url=(\S+)\s", 1, Substring)),
          ""
      )
    | where array_length(url_has_any) == 0 or Url has_any (url_has_any) 
    | extend EventResult=case(
                         LogMessage has "urls",
                         "Success",
                         LogMessage has "security_event" and Action in~("allow", "log", "accept"),
                         "Success",
                         LogMessage has "security_event" and Action in~("block", "deny", "qurantine"),
                         "Failure",
                         ""
                     )
    | where (eventresult == '*' or EventResult =~ eventresult)
    | extend
        EventSeverity=case(
                  LogMessage has "urls",
                  "Informational",
                  LogMessage has "security_event" and Action in~("allow", "log", "accept"),
                  "Informational",
                  LogMessage has "security_event" and Action in~("block", "deny", "qurantine"),
                  "High",
                  ""
              ),
        Ip=extract("(([0-9]{1,3})\\.([0-9]{1,3})\\.([0-9]{1,3})\\.(([0-9]{1,3})))", 1, Url)
    | extend SrcIpAddr = iff(Substring has 'src', extract(@"(src)[ ]{0,1}=[ ]{0,1}\'*((?:[0-9]{1,3}\.){3}[0-9]{1,3})", 2, Substring), "")
    | extend SrcIpAddr = iff(isempty(SrcIpAddr) and Substring has 'src', extract(@'(src)[ ]{0,1}=[ ]{0,1}\"((?:[0-9]{1,3}\.){3}[0-9]{1,3})', 2, Substring), SrcIpAddr)
    | extend SrcIpAddr = iff(isnotempty(SrcIpAddr), SrcIpAddr, extract(@"(src)[ ]{0,1}=[ ]{0,1}\'*\[{0,1}((?i)([0-9a-f]{0,4}:){2,7}[0-9a-f]{0,4})", 2, Substring))
    | extend SrcIpAddr = iff(isempty(SrcIpAddr), extract(@'(src)[ ]{0,1}=[ ]{0,1}\"*\[{0,1}((?i)([0-9a-f]{0,4}:){2,7}[0-9a-f]{0,4})', 2, Substring), SrcIpAddr)
    | extend SrcPortNumber = toint(extract(@"(src)[ ]{0,1}=[ ]{0,1}\'*((?:[0-9]{1,3}\.){3}[0-9]{1,3}):(\d+)+", 3, Substring))
    | extend SrcPortNumber = iff(isempty(SrcPortNumber), toint(extract(@'(src)[ ]{0,1}=[ ]{0,1}\"((?:[0-9]{1,3}\.){3}[0-9]{1,3}):(\d+)+', 3, Substring)), SrcPortNumber)
    | extend SrcPortNumber = iff(isempty(SrcPortNumber), toint(extract(@"(src)[ ]{0,1}=[ ]{0,1}[\']*(((\[{0,1})((((([0-9A-Fa-f]{0,4})))(:{1,1})([0-9A-Fa-f]{0,4})){1,8})(\]{0,1})(:([0-9]{0,5})){0,1}))", 14, Substring)), SrcPortNumber)
    | extend SrcPortNumber = iff(isempty(SrcPortNumber), toint(extract(@'(src)[ ]{0,1}=[ ]{0,1}[\"]*(((\[{0,1})((((([0-9A-Fa-f]{0,4})))(:{1,1})([0-9A-Fa-f]{0,4})){1,8})(\]{0,1})(:([0-9]{0,5})){0,1}))', 14, Substring)), SrcPortNumber)
    | extend DstIpAddr = iff(Substring has 'dst', extract(@"(dst)[ ]{0,1}=[ ]{0,1}\'*((?:[0-9]{1,3}\.){3}[0-9]{1,3})", 2, Substring), "")
    | extend DstIpAddr = iff(isempty(DstIpAddr) and Substring has 'dst', extract(@'(dst)[ ]{0,1}=[ ]{0,1}\"((?:[0-9]{1,3}\.){3}[0-9]{1,3})', 2, Substring), DstIpAddr)
    | extend DstIpAddr = iff(isnotempty(DstIpAddr), DstIpAddr, extract(@"(dst)[ ]{0,1}=[ ]{0,1}\'*\[{0,1}((?i)([0-9a-f]{0,4}:){2,7}[0-9a-f]{0,4})", 2, Substring))
    | extend DstIpAddr = iff(isempty(DstIpAddr), extract(@'(dst)[ ]{0,1}=[ ]{0,1}\"*\[{0,1}((?i)([0-9a-f]{0,4}:){2,7}[0-9a-f]{0,4})', 2, Substring), DstIpAddr)
    | extend DstPortNumber = toint(extract(@"(dst)[ ]{0,1}=[ ]{0,1}\'*((?:[0-9]{1,3}\.){3}[0-9]{1,3}):(\d+)+", 3, Substring))
    | extend DstPortNumber = iff(isempty(DstPortNumber), toint(extract(@'(dst)[ ]{0,1}=[ ]{0,1}\"((?:[0-9]{1,3}\.){3}[0-9]{1,3}):(\d+)+', 3, Substring)), DstPortNumber)
    | extend DstPortNumber = iff(isempty(DstPortNumber), toint(extract(@"(dst)[ ]{0,1}=[ ]{0,1}[\']*(((\[{0,1})((((([0-9A-Fa-f]{0,4})))(:{1,1})([0-9A-Fa-f]{0,4})){1,8})(\]{0,1})(:([0-9]{0,5})){0,1}))", 14, Substring)), DstPortNumber)
    | extend DstPortNumber = iff(isempty(DstPortNumber), toint(extract(@'(dst)[ ]{0,1}=[ ]{0,1}[\"]*(((\[{0,1})((((([0-9A-Fa-f]{0,4})))(:{1,1})([0-9A-Fa-f]{0,4})){1,8})(\]{0,1})(:([0-9]{0,5})){0,1}))', 14, Substring)), DstPortNumber)
    | extend EventType=case(
                       LogMessage has "urls" and Ip != "",
                       "HTTPsession",
                       "WebServerSession"
                   )
    | extend
        temp_SrcMatch=has_any_ipv4_prefix(SrcIpAddr, src_or_any)
        ,
        temp_DstMatch=has_any_ipv4_prefix(DstIpAddr, ipaddr_has_any_prefix)
    | extend ASimMatchingIpAddr=case(
                                array_length(src_or_any) == 0,
                                "-",
                                temp_SrcMatch and temp_DstMatch,
                                "Both",
                                temp_SrcMatch,
                                "SrcIpAddr",
                                temp_DstMatch,
                                "DstIpAddr",
                                "No match"
                            )
    | where ASimMatchingIpAddr != "No match"
    | extend HttpUserAgent = iff(
                             LogMessage has "urls",
                             extract(@"agent='([\S\.\s\S]+)'", 1, Substring),
                             ""
                         ) 
    | where (array_length(httpuseragent_has_any) == 0 or HttpUserAgent has_any(httpuseragent_has_any))
    | extend
        HttpRequestMethod = iff(LogMessage has "urls", extract(@"request: (\w+)\s", 1, Substring), ""),
        FileSHA256 = iff(LogMessage has "security_event", tostring(extract(@"sha256([=]*)(\S+)?", 2, Substring)), ""),
        FileName = iff (
               LogMessage has "security_event",
               tostring(extract(@"name='(\S+)'\s", 1, Substring)),
               ""
           ),
        EventReportUrl=case (
                   LogMessage has "security_event",
                   tostring(extract(@"url=(\S+)\s", 1, Substring)),
                   LogMessage has "urls",
                   tostring(extract(@"request: (\w+)\s(\S+)", 2, Substring)),
                   ""
               ),
        DvcMacAddr=tostring(extract(@"mac=(\S+)\s", 1, Substring))
    | invoke _ASIM_ResolveDvcFQDN('Device')
    | extend 
        Dst=DstIpAddr,
        Src=SrcIpAddr,
        Dvc=DvcHostname,
        IpAddr=SrcIpAddr,
        UserAgent=HttpUserAgent
    | lookup ActionLookup on Action
    | project-away
        LogMessage,
        Parser,
        Epoch,
        EpochTimestamp,
        Device,
        Disposition,
        Action,
        Ip,
        Substring,
        temp_*,
        TenantId,
        SourceSystem,
        Computer,
        _ResourceId,
        MG,
        ManagementGroupName,
        RawData,
        EventTime,
        Facility,
        HostName,
        SeverityLevel,
        ProcessID,
        HostIP,
        ProcessName
  };
  parser(
      starttime=starttime, 
      endtime=endtime,
      srcipaddr_has_any_prefix=srcipaddr_has_any_prefix,
      dstipaddr_has_any_prefix=dstipaddr_has_any_prefix,
      ipaddr_has_any_prefix=ipaddr_has_any_prefix, 
      url_has_any=url_has_any,
      httpuseragent_has_any=httpuseragent_has_any,
      eventresult=eventresult,
      disabled=disabled
  )