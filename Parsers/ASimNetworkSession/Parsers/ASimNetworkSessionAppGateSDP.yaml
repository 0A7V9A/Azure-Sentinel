Parser:
  Title: Network Session ASIM parser for AppGate SDP
  Version: '0.1'
  LastUpdated: May 24, 2021
Product:
  Name: AppGate SDP
Normalization:
  Schema: NetworkSession
  Version: '0.2.2'
References:
  - Title: ASIM Network Session Schema
    Link: https://aka.ms/ASimNetworkSessionDoc
  - Title: ASIM
    Link: https://aka.ms/AboutASIM
Description: |
  This ASIM parser supports normalizing AppGate SDP logs to the ASIM Network Session normalized schema.
ParserName: ASimNetworkSessionAppGateSDP
EquivalentBuiltInParser: _ASim_NetworkSession_AppGateSDP
ParserParams:
  - Name: disabled
    Type: bool
    Default: false
ParserQuery: |
  let parser = (disabled:bool=false) 
  {
    let DirectionLookup = datatable (direction:string, NetworkDirection:string) 
    [
      'up', 'Inbound',
      'down', 'Outbound'
    ];
    let ActionLookup = datatable (DvcOriginalAction:string, DvcAction:string)
    [
      'allow', 'Allow',
      'drop', 'Drop',
      'reject', 'Deny',
      'block', 'Deny',
      'block_report', 'Deny',
      'allow_report', 'Allow'
    ];
    Syslog
    | where ProcessName == "cz-sessiond" or ProcessName == "cz-vpnd"
    | where SyslogMessage has_all ("[AUDIT]","ip_access") // and SyslogMessage has_any ("cz-sessiond", "cz-vpnd")
    | extend type = extract (@'"event_type"\:\"(.*?)\"', 1,  SyslogMessage)
    | where type == "ip_access"
    | parse SyslogMessage with 
        *
        '"action":"' DvcOriginalAction:string '",' // -- to be normalized based on list of values.
        *
        '"client_ip":"' SrcIpAddr:string '",'
        '"client_port":' SrcPortNumber:int ','
        *
        '"destination_ip":"' DstIpAddr:string '",'
        '"destination_port":' DstPortNumber:int ','
        '"direction":"' direction:string '",' // -- to be normalized based on list of values.
        *
        '"distinguished_name_device_id":"' SrcDvcId:string '",'
        '"distinguished_name_user":"' SrcUsername:string '",'
        '"entitlement_token_id":"' NetworkSessionId:string '",'
        *
        '"city_name":"' SrcGeoCity:string '",'
        *
        '"country_name":"' SrcGeoCountry:string '",'
        *
        '"lat":' SrcGeoLatitude:real ','        
        '"lon":' SrcGeoLongitude:real '},'
        *
        '"region_name":"' SrcGeoRegion:string '",'      
        * 
        '"packet_size":' SrcBytes:long ','
        '"protocol":"' NetworkProtocol:string '",'  
        *          
        '"rule_name":"' NetworkRuleName:string '",'  
        '"source_ip":"' SrcNatIpAddr:string '",' 
        '"source_port":"' SrcNatPortNumber:int '",' 
        *
        '"version":' EventProductVersion:string '}'
        *
    | extend 
        SrcDvcIdType = 'AppGateId',
        SrcUsernameType = 'UPN'
    // -- Event fields
    | project-rename 
        DvcHostname = Computer
    | extend 
        EventCount = int(1),
        EventEndTime = TimeGenerated,
        EventStartTime = TimeGenerated,
        EventSchema = 'NetworkSession',
        EventSchemaVersion = '0.2.3',
        EventVendor = 'AppGate',
        EventProduct = 'SDP',
        EventSeverity = 'Informational', // To do based on action
        EventType = 'NetworkSession',
        EventResult = 'Success' // To do based on action
    | lookup DirectionLookup on direction
    | lookup ActionLookup on DvcOriginalAction
    // -- Aliases
    | extend 
        Src = SrcIpAddr,
        Dst = DstIpAddr,
        Dvc = DvcHostname,
        SessionId = NetworkSessionId,
        IpAddr = SrcIpAddr,
        Rule = NetworkRuleName
    | project-away 
        SyslogMessage, type, direction
  };
  parser (disabled)