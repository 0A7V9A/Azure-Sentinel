SchemaVersion: 1.0
DataTypes:
  - DataType: Anomalies
Type: KQL
Provider: Sentinel
EntitiesFilter: 
 Host_OsFamily:
  - Windows
RequiredInputFieldsSets: 
 - - Host_HostName
   - Host_NTDomain
 - - Host_HostName
   - Host_DnsDomain
 - - Host_AzureID
 - - Host_OMSAgentID
BaseQuery: |
  let AnomQuery = (v_Host_Name:string, v_Host_NTDomain:string, v_Host_DnsDomain:string, v_Host_AzureID:string, v_Host_OMSAgentID:string) {
  // get match values
  let matchType = 'HostName';
  let Host_Name = iff(isnotempty(v_Host_Name), v_Host_Name, 'v_Host_Name_NA');
  let Host_NTDomain = iff(isnotempty(v_Host_NTDomain), v_Host_NTDomain, 'v_Host_NTDomain_NA');
  let Host_DnsDomain = iff(isnotempty(v_Host_DnsDomain), v_Host_DnsDomain, 'v_Host_DnsDomain_NA');
  let Host_AzureID = iff(isnotempty(v_Host_AzureID), v_Host_AzureID, 'v_Host_AzureID_NA');
  let Host_OMSAgentID = iff(isnotempty(v_Host_OMSAgentID), v_Host_OMSAgentID, 'v_Host_OMSAgentID_NA');
  // get summary list of entities from anomalies
  let ents = materialize(
  Anomalies
  | project
  Ent = todynamic(Entities),
  Entities,
  AnomalyTemplateName,
  AnomalyProviderName = VendorName,
  AnomalyId = Id,
  AnomalyTemplateId
  | mv-expand Ent
  | where Ent.Type in~ ('host', 'account', 'ip')
  | evaluate bag_unpack(Ent)
  | extend entType = column_ifexists('Type', '')
  | project entType, AnomalyTemplateName, AnomalyProviderName, AnomalyId, AnomalyTemplateId,
  HostName = toupper(column_ifexists('HostName', '')), DnsDomain = toupper(column_ifexists('DnsDomain', '')), NetBiosName = toupper(column_ifexists('NetBiosName', '')), FQDN = toupper(column_ifexists('FQDN', '')), OMSAgentID = tostring(column_ifexists('OMSAgentID', '')), AzureID = tostring(column_ifexists('AzureID', '')), MdatpDeviceId = column_ifexists('MdatpDeviceId', ''), OSFamily = column_ifexists('OSFamily', ''), OSVersion = column_ifexists('OSVersion', ''),
  Name = tolower(column_ifexists('Name', '')), UserSid = tolower(column_ifexists('Sid', '')), AccountName = tolower(column_ifexists('AccountName', '')), UserPrincipalName = tolower(column_ifexists('UserPrincipalName', '')), DisplayName = tolower(column_ifexists('DisplayName', '')), UPNSuffix = tolower(column_ifexists('UPNSuffix', '')), NTDomain = tolower(column_ifexists('NTDomain', '')), AadUserId = tostring(column_ifexists('AadUserId', '')), 
  Address = tostring(column_ifexists('Address', '')), LastIpAddress = tostring(column_ifexists('LastIpAddress', '')), LastExternalIpAddress = tostring(column_ifexists('LastExternalIpAddress', ''))
  | distinct *
  | extend
  Name = iff(Name has @'[', tostring(todynamic(Name)[0]), Name),
  UPNSuffix = iff(UPNSuffix has @'[', tostring(todynamic(UPNSuffix)[0]), UPNSuffix)
  | extend Host = case(
  FQDN has_any (@'.', @'\'), strcat('FQDN', '|', FQDN), 
  isnotempty(HostName) and HostName !has @'.' and isnotempty(DnsDomain), strcat('HostName_DnsDomain', '|', HostName, @'.', DnsDomain),
  isnotempty(NetBiosName), strcat('NetBiosName', '|', NetBiosName),
  isnotempty(AzureID), strcat('AzureID', '|', AzureID),
  isnotempty(OMSAgentID), strcat('OMSAgentID', '|', OMSAgentID),
  isnotempty(MdatpDeviceId), strcat('MdatpDeviceId', '|', MdatpDeviceId),
  isnotempty(HostName), strcat('HostName', '|', HostName),
  'Not Available')
  | extend Account = case(
  UserPrincipalName has @'@', strcat('UserPrincipalName', '|', UserPrincipalName),
  Name has_any (@'@', @'\'), strcat('Name', '|', Name),
  DisplayName has_any (@'@', @'\'), strcat('DisplayName', '|', DisplayName),
  isnotempty(UPNSuffix) and isnotempty(Name) and not(Name has_any (@'@', @'\')), strcat('Name_UPNSuffix', '|', Name, @'@', UPNSuffix),
  isnotempty(NTDomain) and isnotempty(Name) and not(Name has_any (@'@', @'\')), strcat('NTDomain_Name', '|', NTDomain, @'\', Name),
  isnotempty(AadUserId), strcat('AadUserId', '|', AadUserId),
  isnotempty(Name), strcat('Name', '|', Name),
  'Not Available')
  | extend Address = case(
  isnotempty(Address), strcat('Address', '|', Address),
  isnotempty(LastIpAddress), strcat('LastIpAddress', '|', LastIpAddress),
  isnotempty(LastExternalIpAddress), strcat('LastExternalIpAddress', '|', LastExternalIpAddress),
  'Not Available')
  )
  ;
  //Get primary host anomaly ids
  let hostAnoms = ents
  | where (Host has Host_Name and (Host has Host_DnsDomain or Host has Host_NTDomain))
  or AzureID has Host_AzureID
  or OMSAgentID has Host_OMSAgentID
  // need to handle if all we get is the hostname but not get duplicate if we have hostname + dnsdomain or ntdomain
  or Host has Host_Name and Host !has @'.' and not(Host has Host_DnsDomain or Host has Host_NTDomain)
  | distinct AnomalyTemplateId;
  //Get associated entities only from primary host anomaly ids
  let entLists = hostAnoms
  | join kind=inner ents on AnomalyTemplateId 
  | where (Host !has Host_Name and (Host !has Host_DnsDomain or Host !has Host_NTDomain))
  and AzureID !has Host_AzureID
  and OMSAgentID !has Host_OMSAgentID
  // need to handle if all we get is the hostname but not get duplicate if we have hostname + dnsdomain or ntdomain
  or Host !has Host_Name and Host !has @'.' and not(Host has Host_DnsDomain or Host has Host_NTDomain)
  | summarize 
  Host_Set = make_set_if(Host, isnotempty(Host) and entType =~ 'host'), 
  Account_Set = make_set_if(Account, isnotempty(Account) and entType =~ 'account'),
  IP_Set = make_set_if(Address, isnotempty(Address) and entType =~ 'ip');
  //Check each associated entities for Anomalies and provide count
  let hostList = entLists
  | project Host_Set;
  let acctList = entLists
  | project Account_Set;
  let ipList = entLists
  | project IP_Set;
  let entMatch = ents
  | where Host has_any (hostList)
  or Account has_any (acctList)
  or Address has_any (ipList)
  ;
  entMatch
  | extend Entity = case(
  isnotempty(Host) and Host !~ 'NOT AVAILABLE', Host,
  isnotempty(Account) and Account !~ 'NOT AVAILABLE', Account,
  isnotempty(Address) and Address !~ 'NOT AVAILABLE', Address,
  'NOT AVAILABLE')
  | extend AssociatedEntity = tostring(pack("Host_Name", Host_Name, "Host_NTDomain", Host_NTDomain, "Host_DnsDomain", Host_DnsDomain, "Host_AzureID", Host_AzureID, "Host_OMSAgentID", Host_OMSAgentID))
  };
  AnomQuery('{{Host_HostName}}', '{{Host_NTDomain}}', '{{Host_DnsDomain}}', '{{Host_AzureID}}', '{{Host_OMSAgentID}}')
# The queries for the insights.
Insights:
 Id: 32ff1175-860e-4d7e-bc67-7de6cedc9f0c
 DisplayName: Anomalies shared by associated entities (Top 5)
 Description: |
   Using anomalies that include this entity, extract all other entities and provide the count of entities per anomaly type from those associated entities (Top 5).
 DefaultTimeRange: 
  BeforeRange: 12h
  AfterRange: 12h
 TableQuery:
  ColumnsDefinitions:
  - Header: Anomaly
    OutputType: String
    SupportDeepLink: false
  - Header: Entity Count
    OutputType: Number
    SupportDeepLink: true
  - Header: Entity Type
    OutputType: String
    SupportDeepLink: false
  QueriesDefinitions:
  - Filter:    "project-reorder Entity, entType, Host, Account, Address, AnomalyTemplateName, AnomalyProviderName, AnomalyId"
    Summarize: "summarize EntityCount = dcount(Entity), make_set(Entity, 50), make_set(AnomalyId, 50), make_set(AnomalyId, 50), AnomalyCount = count() by AnomName = AnomalyTemplateName, entType | top 5 by EntityCount"
    Project:   "project AnomName, EntityCount, EntityType = entType"
    LinkColumnsDefinitions:
    - ProjectedName: Anomaly Count
      Query: "{{BaseQuery}} | {{RowFilter}}"

 ChartQuery: 
  Title: "Count of entities with a shared anomaly"
  DataSets: 
   - Query: "summarize Count = dcount(AnomalyTemplateName) by Entity = tostring(split(Entity,'|')[1]) | as counts | extend Xcol = 1000*toscalar((counts | summarize dcount(Entity)))"
     XColumnName: "Xcol"
     YColumnName: "Count"
     LegendColumnName: "Entity"
  Type: BarChart

 AdditionalQuery:
  Text: "See all associated entities"
  Query: "extend SourceField = tostring(split(Entity, '|')[0]), Entity = tostring(split(Entity,'|')[1]) | project-reorder SourceField, Entity, entType, Host, Account, Address,AnomalyTemplateName, AnomalyProviderName, AnomalyId, AssociatedEntity"
version: 1.0.0