// Name: Mass Secret retrievel from keyvault
//
// Id: 24f8c234-d1ff-40ec-8b73-96b17a3a9c1c
//
// Description: This query looks for mass secret retrieval from keyvault from a single user. 
// Mass secret retrival crossing certain threshold is an indication of credential dump operation or mis-configured application.
//
// Severity: Low
//
// QueryFrequency: 1h
//
// QueryPeriod: 1d
//
// AlertTriggerOperator: gt
//
// AlertTriggerThreshold: AlertTriggerThreshold
//
// DataSource: #AzureDiagnostics
//
// Tactics: #CredentialAccess, #Collection
//
let starttime =1d;
let endtime = 1h;
let AlertTriggerThreshold = 25;
let OperationList = dynamic(
["SecretGet", "KeyGet", "VaultGet"]);
AzureDiagnostics
| where TimeGenerated between (ago(starttime)..ago(endtime))
| where ResourceType == "VAULTS" and ResultType == "Success"
| where OperationName in (OperationList) 
| summarize count() by identity_claim_http_schemas_xmlsoap_org_ws_2005_05_identity_claims_upn_s, OperationName
| where count_ > AlertTriggerThreshold  
| join (
AzureDiagnostics
| where TimeGenerated between (ago(starttime)..ago(endtime))
| where ResourceType == "VAULTS" and ResultType == "Success"
| where OperationName in (OperationList)   
) on identity_claim_http_schemas_xmlsoap_org_ws_2005_05_identity_claims_upn_s 
| project TimeGenerated , Resource, OperationName , id_s, CallerIPAddress ,identity_claim_http_schemas_xmlsoap_org_ws_2005_05_identity_claims_upn_s,
requestUri_s, clientInfo_s