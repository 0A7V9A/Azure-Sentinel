// Name: Time Series Anomaly detection for Total volume in network logs- MultiVendor
// Description: This query will identify anamalous spike in network traffic logs as compared to baseline or normal historical pattern. 
// The query leverages KQL built-in anomaly detection algorithm find large deviations from baseline pattern. 
// Any sudden increase in network traffic volume may be an indication of data exfiltration attempts and should be investigated.
//
// Id: 06a9b845-6a95-4432-a78b-83919b28c375
//
// DataSource: #CommonSecurityLog
//
// Tactics: #Exfiltration
//
let starttime = 30d;
let endtime = 1d;
let timeframe = 1h;
let TimeSeriesData= CommonSecurityLog 
| where isnotempty(DestinationIP) and isnotempty(SourceIP)
| where TimeGenerated between (ago(starttime)..ago(endtime))
| project TimeGenerated,SourceIP, DestinationIP, DeviceVendor
| make-series Total=count() on TimeGenerated from ago(starttime) to ago(endtime) step timeframe by DeviceVendor;
//Filtering specific records associated with spikes as outliers
TimeSeriesData
| extend (anomalies, score, baseline) = series_decompose_anomalies(Total, 1.5, -1, 'linefit')
| mv-expand Total to typeof(double), TimeGenerated to typeof(datetime), anomalies to typeof(double),score to typeof(double), baseline to typeof(long)
| where anomalies > 0
| project DeviceVendor,TimeGenerated, Total, baseline, anomalies, score