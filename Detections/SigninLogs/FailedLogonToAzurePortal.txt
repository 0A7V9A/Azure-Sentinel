// Name: Failed login attempts to Azure Portal
//
// Description: This query over Azure AD sign-in activity highlights failed login attempts to Azure Portal 
//
// DataSource: #SigninLogs
//
// Techniques: #InitialAccess
let timeRange = ago(30d);
SigninLogs
| where TimeGenerated >= timeRange 
| extend OS = DeviceDetail.operatingSystem, Browser = DeviceDetail.browser
| extend StatusCode = tostring(Status.errorCode), StatusDetails = tostring(Status.additionalDetails)
| extend State = tostring(LocationDetails.state), City = tostring(LocationDetails.city)
| where  AppDisplayName contains "Azure Portal"
| where ResultType !in ("0", "50125", "50140")
// 0 - successful logon
// 50125 - Sign-in was interrupted due to a password reset or password registration entry.
// 50140 - This error occurred due to 'Keep me signed in' interrupt when the user was signing-in
| summarize makelist(TimeGenerated), makeset(IPAddress), makeset(OS), makeset(Browser), makeset(City), count() by UserDisplayName, UserPrincipalName, AppDisplayName, ResultType, ResultDescription, StatusCode, StatusDetails, Location, State
| project UserDisplayName, UserPrincipalName, AppDisplayName, AttemptCount = arraylength(list_TimeGenerated), list_TimeGenerated, DistinctIPCount = arraylength(set_IPAddress), set_IPAddress, ResultType, ResultDescription, StatusCode, StatusDetails, set_OS, set_Browser, Location, State, set_City 
| sort by AttemptCount
