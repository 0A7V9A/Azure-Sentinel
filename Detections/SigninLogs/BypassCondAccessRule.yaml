id: 3af9285d-bb98-4a35-ad29-5ea39ba0c628
name: Attempt to Bypass conditional access rule in Azure AD
description: |
  'Identifies an attempt to Bypass conditional access rule in Azure Active Directory.
  The ConditionalAccessStatus column value details if there was an attempt to bypass Conditional Access
  or if the Conditional access rule was not satisfied (ConditionalAccessStatus == 1).
  References: 
  https://docs.microsoft.com/en-us/azure/active-directory/conditional-access/overview
  https://docs.microsoft.com/en-us/azure/active-directory/reports-monitoring/concept-sign-ins
  https://docs.microsoft.com/en-us/azure/active-directory/reports-monitoring/reference-sign-ins-error-codes
  ConditionalAccessStatus == 0 // Success
  ConditionalAccessStatus == 1 // Failure
  ConditionalAccessStatus == 2 // Not Applied
  ConditionalAccessStatus == 3 // unknown'
severity: Low
requiredDataConnectors:
  - connectorId: AzureActiveDirectory
    dataTypes:
      - SigninLogs
queryFrequency: 1d
queryPeriod: 1d
triggerOperator: gt
triggerThreshold: 0
tactics:
  - InitialAccess
  - Persistence
relevantTechniques:
  - T1078
  - T1098
query: |

  let timeRange = ago(1d);
  SigninLogs
  | where TimeGenerated >= timeRange
  // Conditional access rule was not satisfied when == 1
  | where ConditionalAccessStatus == 1 or ConditionalAccessStatus =~ "failure"
  | extend OS = DeviceDetail.operatingSystem, Browser = DeviceDetail.browser 
  | extend StatusCode = tostring(Status.errorCode), StatusDetails = tostring(Status.additionalDetails)
  | extend Region = tostring(LocationDetails["countryOrRegion"]), State = tostring(LocationDetails["state"]), City = tostring(LocationDetails["city"])
  | extend ConditionalAccessPol0Name = tostring(ConditionalAccessPolicies[0].displayName)
  | extend ConditionalAccessPol1Name = tostring(ConditionalAccessPolicies[1].displayName)
  | extend ConditionalAccessPol2Name = tostring(ConditionalAccessPolicies[2].displayName)
  | summarize StartTimeUtc = min(TimeGenerated), EndTimeUtc = max(TimeGenerated) by UserPrincipalName, IPAddress, AppDisplayName, ResultDescription, StatusCode, StatusDetails, tostring(Browser), tostring(OS), Region, State, City, ConditionalAccessPol0Name, ConditionalAccessPol1Name, ConditionalAccessPol2Name
  | project StartTimeUtc, EndTimeUtc, UserPrincipalName, IPAddress, AppDisplayName, ResultDescription, StatusCode, StatusDetails, tostring(Browser), tostring(OS), Region, State, City, ConditionalAccessPol0Name, ConditionalAccessPol1Name, ConditionalAccessPol2Name
  | extend timestamp = StartTimeUtc, AccountCustomEntity = UserPrincipalName, IPCustomEntity = IPAddress
