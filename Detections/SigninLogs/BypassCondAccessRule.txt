// Name: Attempt to Bypass conditional access rule
//
// Description: This query over Azure AD sign-in activity highlights attempt to Bypass conditional access rule. The  ConditionalAccessStatus column value in the logs details if there was an attempt to bypass Conditional Access or if the Conditional access rule was not satisfied (ConditionalAccessStatus == 1 )
// ConditionalAccessStatus == 0     //Success
// ConditionalAccessStatus == 1     // Failure
// ConditionalAccessStatus == 2      // Not Applied
// ConditionalAccessStatus == 3      // unknown
//
// DataSource: #SigninLogs
//
// Techniques: #InitialAccess
let timeRange = ago(30d);
SigninLogs
| where TimeGenerated >= timeRange
| where ConditionalAccessStatus == 1 
//Conditional access rule was not satisfied
| extend OS = DeviceDetail.operatingSystem, Browser = DeviceDetail.browser 
| extend StatusCode = tostring(Status.errorCode), StatusDetails = tostring(Status.additionalDetails)
| extend locationString= strcat(tostring(LocationDetails["countryOrRegion"]), "/", tostring(LocationDetails["state"]), "/", tostring(LocationDetails["city"]))
| extend ConditionalAccessPol0Name = tostring(ConditionalAccessPolicies[0].displayName)
| extend ConditionalAccessPol1Name = tostring(ConditionalAccessPolicies[1].displayName)
| extend ConditionalAccessPol2Name = tostring(ConditionalAccessPolicies[2].displayName)
| summarize makeset(AppDisplayName) by UserPrincipalName  , locationString, IPAddress ,tostring(Browser),tostring(OS),ConditionalAccessPol1Name , ConditionalAccessPol0Name
