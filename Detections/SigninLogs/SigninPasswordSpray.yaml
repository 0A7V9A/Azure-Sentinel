id: 48607a29-a26a-4abf-8078-a06dbdd174a4
name: Password spray attack against Azure AD application
description: |
  'Identifies evidence of password spray activity against Azure AD applications by looking for failures from multiple accounts from the same
  IP address within a time window. If the number of accounts breaches the threshold just once, all failures from the IP address within the time range
  are bought into the result. Details on whether there were successful authentications by the IP address within the time window are also included.
  This can be an indicator that an attack was successful.
  The default failure acccount threshold is 5, Default time window for failures is 20m and default look back window is 3 days
  References: https://docs.microsoft.com/en-us/azure/active-directory/reports-monitoring/reference-sign-ins-error-codes.'
severity: Medium
requiredDataConnectors:
  - connectorId: AzureActiveDirectory
    dataTypes:
      - SigninLogs
queryFrequency: 1d
queryPeriod: 1d
triggerOperator: gt
triggerThreshold: 0
tactics:
  - CredentialAccess
relevantTechniques:
  - T1110
query: |

    let timeRange = 3d;
    let authenticationWindow = 20m;
    let authenticationThreshold = 5;
    // collect window threshold breaches
    SigninLogs
    | where TimeGenerated > ago(timeRange)
    | where ResultType in (50126, 50053) // invalid password, account is locked - too many sign ins
    | summarize StartTimeUtc = min(TimeGenerated), EndTimeUtc = max(TimeGenerated), make_set(ClientAppUsed), count() by bin(TimeGenerated, authenticationWindow), IPAddress, AppDisplayName, Identity
    | summarize FailedIdentityCount = dcount(Identity) by bin(TimeGenerated, authenticationWindow), IPAddress, AppDisplayName
    | where FailedIdentityCount >= authenticationThreshold
    | summarize WindowThresholdBreaches = count() by IPAddress
    | join kind= inner (
    // where we breached a threshold, join the details back on all failure data
    SigninLogs
    | where TimeGenerated > ago(timeRange)
    | where ResultType in (50126, 50053)
    | summarize StartTimeUtc = min(TimeGenerated), EndTimeUtc = max(TimeGenerated), make_set(ClientAppUsed), FailureCount = count() by IPAddress, AppDisplayName, Identity
    | summarize StartTimeUtc = min(StartTimeUtc), EndTimeUtc = max(EndTimeUtc), make_set(Identity), make_set(set_ClientAppUsed), make_list(FailureCount) by IPAddress, AppDisplayName
    | extend FailedIdentityCount = arraylength(set_Identity)
    ) on IPAddress
    | project IPAddress, StartTimeUtc, EndTimeUtc, TargetedApplication=AppDisplayName, FailedIdentityCount, Identities=set_Identity, ClientAppsUsed=set_set_ClientAppUsed, FailureCountByIdentity=list_FailureCount, WindowThresholdBreaches
    | join kind= inner (
    SigninLogs // get data on success vs. failure history for each IP
    | where TimeGenerated > ago(timeRange)
    | where ResultType in (0, 50076, 50158, 50074, 50126, 50053) // success or failure types
    | summarize GlobalSuccessIdentityCount = dcountif(Identity, (ResultType in (0, 50076, 50158, 50074))), GlobalFailIdentityCount = dcountif(Identity, (ResultType in (50126, 50053))) by IPAddress
    | where GlobalFailIdentityCount > GlobalSuccessIdentityCount // where the number of failed identities is greater than success - eliminates FPs from IPs who authenticate successfully alot and as a side effect have alot of failures
    ) on IPAddress
    | project-away IPAddress1
    | extend IPCustomEntity = IPAddress