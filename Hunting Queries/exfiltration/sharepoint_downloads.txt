//Shows volume of documents uploaded to or downloaded from Sharepoint by new IP addresses. 
//In stable environments such connections by new IPs may be unauthorized, especially if associated with spikes in volume which could be associated with large-scale document exfiltration.

let historicalActivity=
OfficeActivity
| where  RecordType == "SharePointFileOperation"
| where Operation in ("FileDownloaded", "FileUploaded")
| where TimeGenerated between(ago(30d)..ago(7d))
| summarize historicalCount=count() by ClientIP;
let recentActivity = OfficeActivity
| where  RecordType == "SharePointFileOperation"
| where Operation in ("FileDownloaded", "FileUploaded")
| where TimeGenerated > ago(1d) 
| summarize recentCount=count() by ClientIP;
recentActivity | join kind= leftanti (
   historicalActivity 
) on ClientIP; 


//Shows volume of documents uploaded to or downloaded from Sharepoint by user agent. 
//Tracking via user agent is one way to differentiate between types of connecting device. 
//In homogeneous enterprise environments the user agent associated with an attacker device may stand out as unusual. 
//In stable environments such connections by new IPs may be unauthorized, especially if associated with spikes in volume which could be associated with large-scale document exfiltration.
// Sharepoint - files downloaded/uploaded by new user agent
let historicalActivity=
OfficeActivity
| where  RecordType == "SharePointFileOperation"
| where Operation in ("FileDownloaded", "FileUploaded")
| where TimeGenerated between(ago(30d)..ago(7d))
| summarize historicalCount=count() by UserAgent;
let recentActivity = OfficeActivity
| where  RecordType == "SharePointFileOperation"
| where Operation in ("FileDownloaded", "FileUploaded")
| where TimeGenerated > ago(1d) 
| summarize recentCount=count() by UserAgent;
recentActivity | join kind= leftouter (
   historicalActivity 
) on UserAgent; 


// Sharepoint -New user agents associated with a clientIP for sharepoint file uploads/downloads
let historicalUA=
OfficeActivity
| where  RecordType == "SharePointFileOperation"
| where Operation in ("FileDownloaded", "FileUploaded")
| where TimeGenerated between(ago(30d)..ago(7d))
// DEMO | where TimeGenerated between(datetime(2018-05-25)..datetime(2018-05-29 21:00)) // fake history
| summarize by ClientIP, UserAgent;
let recentUA = OfficeActivity
| where  RecordType == "SharePointFileOperation"
| where Operation in ("FileDownloaded", "FileUploaded")
| where TimeGenerated > ago(1d) 
// DEMO | where TimeGenerated between (datetime(2018-05-29 21:30)..datetime(2018-05-29 21:45)) 
| summarize by ClientIP, UserAgent;
recentUA | join kind=leftanti (
   historicalUA 
) on ClientIP, UserAgent; 
