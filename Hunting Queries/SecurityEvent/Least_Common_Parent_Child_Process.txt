// Name: Least Common Parent And Child Process Pairs
// Id: 3712595d-6f47-416b-963a-605201ed2764
// Description: Looks across your environment for least common Parent/Child process combinations.  Will possibly find some malicious activity disguised as well known process names.  By ZanCo
// DataSource: #SecurityEvent
// Tactics: #InitialAccess, #Execution, #Persistance
let Whitelist = dynamic (['foo.exe', 'baz.exe']);
let Sensitivity = 5;
let StartDate = ago(7d);
let Duration = 7d;
SecurityEvent
| where EventID == 4688 and TimeGenerated > StartDate and TimeGenerated < (StartDate + Duration) and isnotnull(ParentProcessName)
| extend ProcArray = split(NewProcessName, '\\'), ParentProcArray = split(ParentProcessName, '\\')
// ProcArrayLength is Folder Depth
| extend ProcArrayLength = arraylength(ProcArray), ParentProcArrayLength = arraylength(ParentProcArray)
| extend LastIndex = ProcArrayLength - 1, ParentLastIndex = ParentProcArrayLength - 1
| extend Proc = ProcArray[LastIndex], ParentProc = ParentProcArray[ParentLastIndex]
| where Proc !in (Whitelist)
| extend ParentChildPair = strcat(ParentProc , ' > ', Proc)
| summarize TimesSeen = count(), HostCount = dcount(Computer), Hosts = makeset(Computer), UserCount = dcount(SubjectUserName), Users = makeset(SubjectUserName) by ParentChildPair
| where TimesSeen < Sensitivity