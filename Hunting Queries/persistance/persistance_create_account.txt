// Name: Sunmary of users created using uncommon & undocumented commandline switches
// Description: Summarizes users of uncommon & undocumented commandline switches to create persistance
// User accounts may be created to achieve persistence on a machine.
// Read more here: https://attack.mitre.org/wiki/Technique/T1136
// Query for users being created using "net user" command
// "net user" commands are noisy, so needs to be joined with another signal -
// e.g. in this example we look  (e.g. /ad instead of /add)
// Tags: #LateralMovement 
SecurityEvent
| where EventID==4688
| project  EventTime=TimeGenerated, ComputerName=Computer,AccountName=SubjectUserName, 
    AccountDomain=SubjectDomainName, FileName=reverse(substring(reverse(NewProcessName), 0, 
    indexof(reverse(NewProcessName), "\\"))), ProcessCommandLine = CommandLine, 
    FolderPath = "", InitiatingProcessFileName=ParentProcessName,
    InitiatingProcessCommandLine="",InitiatingProcessParentFileName=""
| where FileName in~ ("net.exe", "net1.exe") and EventTime > ago(3d)
| parse kind=regex flags=iU ProcessCommandLine with * "user " CreatedUser " " * "/ad"
| where not(FileName =~ "net1.exe" and InitiatingProcessFileName =~ "net.exe" and replace("net", "net1", InitiatingProcessCommandLine) =~ ProcessCommandLine)
| extend CreatedOnLocalMachine=(ProcessCommandLine !contains "/do")
| where ProcessCommandLine contains "/add" or (CreatedOnLocalMachine == 0 and ProcessCommandLine !contains "/domain")
| summarize MachineCount=dcount(ComputerName) by CreatedUser, CreatedOnLocalMachine, InitiatingProcessFileName, FileName, ProcessCommandLine, InitiatingProcessCommandLine 
