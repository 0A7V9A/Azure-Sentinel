// Name: Sharepoint downloads
//
// Id: e8ae1375-4640-430c-ae8e-2514d09c71eb
//
// Description: Shows volume of documents uploaded to or downloaded from Sharepoint by new IP addresses.
// In stable environments such connections by new IPs may be unauthorized, especially if associated with spikes in volume which could be associated with large-scale document exfiltration.
//
// DataSource: #OfficeActivity
//
// Tactics: #Exfiltration
//
let historicalUA=
OfficeActivity
| where  RecordType == "SharePointFileOperation"
| where Operation in ("FileDownloaded", "FileUploaded")
| where TimeGenerated between(ago(30d)..ago(7d))
| summarize by ClientIP, UserAgent;
let recentUA = OfficeActivity
| where  RecordType == "SharePointFileOperation"
| where Operation in ("FileDownloaded", "FileUploaded")
| where TimeGenerated > ago(1d) 
| summarize by ClientIP, UserAgent;
recentUA | join kind=leftanti (
   historicalUA 
) on ClientIP, UserAgent
// Some OfficeActivity records do not contain ClientIP information - exclude these for fewer results:
| where not(isempty(ClientIP)); 
