id: d2e6f31b-add1-4f44-b54d-1975a5605c1d
name: Web shell command alert enrichment
description: |
  'Extracts MDATP Alerts that indicate a command was executed by a web shell. Uses time window based querying to idneitfy the potential web shell location on the server, then enriches with Attacker IP and User Agent'
requiredDataConnectors:
  - connectorId: AzureSecurityCenter
    dataTypes:
      - SecurityAlert
  - connectorId: MicrosoftCloudAppSecurity
    dataTypes:
      - SecurityAlert
  - connectorId: AzureMonitor(IIS)
    dataTypes:
      - W3CIISLog
tactics:
  - Privilege Escalation
  - Persistence
query: |
  let timeRange = 3d;  
  let lookupWindow = 5min;  
  let lookupBin = lookupWindow / 2.0;
  let scriptExtensions = dynamic([".php", ".jsp", ".js", ".aspx", ".asmx", ".asax", ".cfm", ".shtml"]);
  //Extract all MDATP security alert data for alerts associated with command execution by a web shell
  let alldata = materialize(SecurityAlert
  | where TimeGenerated >= ago(timeRange)
  | where ProviderName =~ "MDATP"  
  | where DisplayName has_any("Possible IIS web shell", "Possible IIS compromise", "Suspicious processes indicative of a web shell", "A suspicious web script was created")
  | extend alertData = parse_json(Entities)  
  | extend recordGuid = new_guid()  
  | mvexpand alertData)  
  ;  
  //Extract process data from alert JSON
  let filedata = alldata  
  | extend id = tostring(alertData.$id)  
  | extend type = alertData.Type  
  | extend imagename = alertData.Name  
  | where type =~ "file"
  | where imagename != ""
  | extend imagefileref = id
  ;
  //Extract commandline data from alert JSON
  let commanddata = alldata  
  | extend type = alertData.Type  
  | extend commandline = tostring(alertData.CommandLine)  
  | extend creationtime = tostring(alertData.CreationTimeUtc)  
  | where type =~ "process"  
  | extend imagefileref = tostring(alertData.ImageFile.$ref);
  //Join file and command data together, we have no parsed the JSON object into a table we can work with
  filedata  
  | join kind=leftouter (  
  commanddata  
  | extend id = imagefileref  
  ) on imagefileref  
  | project recordGuid, TimeGenerated, creationtime, imagename, commandline, TimeKey = bin(TimeGenerated, lookupBin) 
  | where commandline != "" 
  | extend Start = TimeGenerated 
  //Join to W3CIISLog using a time windows and key to find files accessed during the time that the commandline was executed
  | join kind=inner ( 
  W3CIISLog
  | where TimeGenerated >= ago(timeRange)
  | where csUriStem has_any(scriptExtensions)
  | extend splitUriStem = split(csUriStem, "/")  
  | extend FileName = splitUriStem[-1] | extend firstDir = splitUriStem[-2]  
  | extend TimeKey = range(bin(TimeGenerated-lookupWindow, lookupBin), bin(TimeGenerated, lookupBin),lookupBin)  
  | mv-expand TimeKey to typeof(datetime)  
  | summarize StartTime=min(TimeGenerated), EndTime=max(TimeGenerated) by Site=sSiteName, AttackerIP=cIP, AttackerUserAgent=csUserAgent, csUriStem, filename=tostring(FileName), tostring(firstDir), TimeKey 
  ) on TimeKey 
  | where (Start - EndTime) between (0min .. lookupWindow) 
  | extend attackerP = pack(AttackerIP, AttackerUserAgent)  
  | extend timestamp = StartTime, IPCustomEntity = AttackerIP
  | summarize Site=make_set(Site), Attacker=make_bag(attackerP) by csUriStem, filename, tostring(imagename), tostring(commandline), timestamp, IPCustomEntity
  | project Site, ShellLocation=csUriStem, ShellName=filename, ParentProcess=imagename, CommandLine=commandline, Attacker, timestamp, IPCustomEntity