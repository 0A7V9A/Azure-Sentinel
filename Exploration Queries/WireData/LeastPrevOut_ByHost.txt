// Id: 8c00a2a0-43d3-45a9-aa2e-f73deb0abfbb
// DisplayName: WireData Least Prevalent Outbound Connections by Host
// Description: Bottom 10 (least prevalent) for WireData Outbound event by a given Host (set time range to +-30m when running the query)
// InputEntityType: Host
// InputFields: [HostName]
// OutputEntityTypes: [Ip, Process]
// QueryPeriodBefore: 30m
// QueryPeriodAfter: 30m
// DataSource: #WireData
// Tactics: #Probing, #Discovery, #LateralMovement, #Collection

let GetWireDataOutboundWithHost = (v_Host_HostName:string){
WireData
| where Direction == 'Outbound' 
| where Computer has v_Host_HostName
| summarize min(SessionStartTime), count() by ProcessName, RemoteIP, RemoteIPCountry, RemoteIPLatitude, RemoteIPLongitude, ProcessID
| top 10 by count_ asc
| project min_SessionStartTime, ProcessName, RemoteIP, RemoteIPCountry, RemoteIPLatitude, RemoteIPLongitude, ProcessID
| project-rename IP_Address=RemoteIP, IP_Location_CountryName=RemoteIPCountry, IP_Location_Latitude=RemoteIPLatitude, IP_Location_Longitude=RemoteIPLongitude, Process_ImageFile_FullPath=ProcessName, Process_ProcessId=ProcessID
};
// change <HostName> value below
GetWireDataOutboundWithHost('<HostName>')