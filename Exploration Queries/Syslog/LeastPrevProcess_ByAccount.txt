// Name: Least Prevalent Process by Account
// Description: Bottom 10 process by Account in range of +-1h
//
// Id: 78abcff3-8674-4dfc-aad7-d4ed89ec5f7f
//
// Entity: Account
// Input: Account, AccountName
// Output: Process
//
// QueryPeriod: +-1h, change as needed
//
// Data Source: Syslog
//
// Tactics: #Execution, #Persistence, #Discovery, #Lateral Movement, #Collection
//
let GetSysLogEventsByAccount = (suspiciousEventTime:datetime, v_Account:string){
let v_StartTime = suspiciousEventTime-1h;
let v_EndTime = suspiciousEventTime+1h;
Syslog
| where EventTime between (v_StartTime .. v_EndTime)
| where SyslogMessage has v_Account
| summarize min(EventTime), max(EventTime), count() by Computer, HostName, HostIP, ProcessName
| top 10 by count_ asc nulls last 
| project min_EventTime, max_EventTime, Computer, HostIP, ProcessName, count_
};
// change datetime value and <processname> value below
GetSysLogEventsByAccount(datetime('2019-01-30T10:36:07Z'), "<accountname>")