// Name: Least Prevalent Process on Host
// Description: Bottom 10 process on host in range of +-1h
//
// Entity: Host
// Input: Host, HostName
// Output: Process
//
// QueryPeriod: +-1h, change as needed
//
// Data Source: Syslog
//
// Tactics: #Execution, #Persistence, #Discovery, #Lateral Movement, #Collection
//
let GetSysLogEventsOnHost = (suspiciousEventTime:datetime, v_Host:string){
let v_StartTime = suspiciousEventTime-1h;
let v_EndTime = suspiciousEventTime+1h;
Syslog
| where EventTime between (v_StartTime .. v_EndTime)
| where Computer has v_Host
| summarize min(EventTime), max(EventTime), count() by Computer, HostName, HostIP, ProcessName
| top 10 by count_ asc nulls last 
| project min_EventTime, max_EventTime, Computer, HostIP, ProcessName, count_
};
// change datetime value and <processname> value below
GetSysLogEventsOnHost(datetime('2019-01-30T10:36:07Z'), "<hostname>")