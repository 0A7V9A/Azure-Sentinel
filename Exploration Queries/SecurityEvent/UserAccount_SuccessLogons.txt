// Id: 8a697f4c-04af-4198-a6d3-ce5dc3acc8dd
// DisplayName: User Account Success logons
// Description: Successful logons by a given user during the time of or recently after compromise (set time range to +-1h when running the query)
// InputEntityType: Account
// InputFields: [Name]
// OutputEntityTypes: [Host]
// QueryPeriodBefore: 1h
// QueryPeriodAfter: 1h
// DataSource: #SecurityEvent
// Tactics: #Discovery, #LateralMovement

let GetAllLogonsForUser = (v_Account_Name:string){
SecurityEvent
| where EventID == 4624
| where AccountType == 'User'
| where tolower(Account) contains tolower(v_Account_Name)
| summarize min(TimeGenerated), max(TimeGenerated) by Computer
| project min_TimeGenerated, max_TimeGenerated, Computer
| project-rename Host_UnstructuredName=Computer
| top 10 by min_TimeGenerated asc nulls last
};
// change <Name> value below
GetAllLogonsForUser(tolower('<Name>'))