id: bf0cde21-0c41-48f6-a40c-6b5bd71fa106
name: AWS Guard Duty Alert
description: 'Amazon GuardDuty is a threat detection service that continuously monitors your AWS accounts and workloads for malicious activity and delivers detailed security findings for visibility and remediation. This templates create an alert for each Amazon GuardDuty finding.'
severity: Medium
status: Available
requiredDataConnectors:
  - connectorId: AWSS3
    dataTypes:
      - AWSGuardDuty
queryFrequency: 5h
queryPeriod: 5h
triggerOperator: gt
triggerThreshold: 0
tactics: []
relevantTechniques: []
query:
  AWSGuardDuty
  | extend findingTokens = split(ActivityType, ":")
  | extend ThreatPurpose=findingTokens[0], findingTokens=split(findingTokens[1], "/")
  | extend ResourceTypeAffected=findingTokens[0], findingTokens= split(findingTokens[1], ".")
  | extend ThreatFamilyName=findingTokens[0], findingTokens=split(findingTokens[1], "!")
  | extend DetectionMechanism=findingTokens[0], Artifact=findingTokens[1]
  | extend UniqueFindingId = Id
  | extend AWSAcoundId = AccountId
  | extend Severity=case(
    Severity > 7.0, "High",
    Severity between (4.0 .. 6.9), "Medium",
    Severity between (1.0 .. 3.9), "Low",
    "Unknown"
    )
  // Pull out resource details we can extract entities from
  // https://docs.aws.amazon.com/guardduty/latest/APIReference/API_Resource.html
  | extend ResourceDetailsObject=case(
    isnotempty(ResourceDetails.accessKeyDetails), ResourceDetails.accessKeyDetails,
    isnotempty(ResourceDetails.containerDetails), ResourceDetails.containerDetails,
    isnotempty(ResourceDetails.ebsVolumeDetails), ResourceDetails.ebsVolumeDetails,
    isnotempty(ResourceDetails.ecsClusterDetails), ResourceDetails.ecsClusterDetails,
    isnotempty(ResourceDetails.eksClusterDetails), ResourceDetails.eksClusterDetails,
    isnotempty(ResourceDetails.instanceDetails), ResourceDetails.instanceDetails,
    isnotempty(ResourceDetails.kubernetesDetails), ResourceDetails.kubernetesDetails,
    isnotempty(ResourceDetails.lambdaDetails), ResourceDetails.lambdaDetails,
    isnotempty(ResourceDetails.rdsDbInstanceDetails), ResourceDetails.rdsDbInstanceDetails,
    isnotempty(ResourceDetails.rdsDbUserDetails), ResourceDetails.rdsDbUserDetails,
    isnotempty(ResourceDetails.s3BucketDetails), ResourceDetails.s3BucketDetails,
    dynamic(null)
    )
  // Pull out service action details we can extract entities from
  // https://docs.aws.amazon.com/guardduty/latest/APIReference/API_Action.html
  | extend ActionDetailsObject=case(
    isnotempty(ServiceDetails.action.awsApiCallAction), ServiceDetails.action.awsApiCallAction,
    isnotempty(ServiceDetails.action.dnsRequestAction), ServiceDetails.action.dnsRequestAction,
    isnotempty(ServiceDetails.action.kubernetesApiCallAction), ServiceDetails.action.kubernetesApiCallAction,
    isnotempty(ServiceDetails.action.networkConnectionAction), ServiceDetails.action.networkConnectionAction,
    isnotempty(ServiceDetails.action.portProbeAction), ServiceDetails.action.portProbeAction,
    isnotempty(ServiceDetails.action.rdsLoginAttemptAction), ServiceDetails.action.rdsLoginAttemptAction,
    dynamic(null)
    )
  | extend RemoteIpAddress = max_of(
    tostring(ServiceAction.remoteIpDetails.ipAddressV4),
    tostring(parse_json(ServiceAction.sourceIPs)[0])
    )
  | extend AccountUpn = case(
    AccessKeyDetails.userType == "IAMUser", AccessKeyDetails.userName,
    AccessKeyDetails.userType == "AssumedRole", split(AccessKeyDetails.principalId, ":", 1)[0],
    isnotempty(RdsDbUserDetails.user), RdsDbUserDetails.user,
    isnotempty(KubernetesDetails.kubernetesUserDetails.username), KubernetesDetails.kubernetesUserDetails.username,
    ""
    )
  | extend AccountName = split(AccountUpn, "@", 0)[0]
  | extend UPNSuffix = split(AccountUpn, "@", 1)[0]
  | extend LocalIpAddress = ActionDetailsObject.localIpDetails.ipAddressV4
  | extend RemoteAWSAccountId = ActionDetailsObject.remoteAccountDetails.accountId
  | extend AWSUserName = ResourceDetailsObject.userName
  // Clean up the output
  | project-away
    findingTokens,
    ActivityType,
    Id,
    AccountId,
    TimeGenerated,
    TenantId,
    SchemaVersion,
    Region,
    Partition
version: 1.0.3
entityMappings:
  - entityType: Account
    fieldMappings:
      - identifier: Name
        columnName: AccountName
      - identifier: UPNSuffix
        columnName: UPNSuffix
      - identifier: ObjectGuid
        columnName: RemoteAWSAccountId
  - entityType: IP
    fieldMappings:
      - identifier: Address
        columnName: RemoteIpAddress
      - identifier: Address
        columnName: LocalIpAddress
customDetails:
  ThreatPurpose: ThreatPurpose
  ResourceTypeAffected: ResourceTypeAffected
  UniqueFindingId: UniqueFindingId
  ThreatFamilyName: ThreatFamilyName
  DetectionMechanism: DetectionMechanism
alertDetailsOverride:
  alertDisplayNameFormat: '{{Title}}'
  alertDescriptionFormat: '{{Description}}'
  alertTacticsColumnName: ThreatPurpose
  alertSeverityColumnName: Severity
kind: Scheduled
