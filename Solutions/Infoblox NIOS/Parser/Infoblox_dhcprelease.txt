// Title:           Infoblox parser for type - DHCPRELEASE
// Author:          Microsoft
// Version:         1.1
// Last Updated:    20/05/2022
// Comment:         Updated to only parse the MSG (RFC3164) part of the Syslog message. Excluded the Header.
//
// DESCRIPTION:
// This parser comprised of function - Infoblox_dhcpreleased
//
// USAGE:
// USAGE:
// 1. Open Log Analytics/Azure Sentinel Logs blade. Copy the query below and paste into the Logs query window. 
// 2. Click the Save button above the query. A pane will appear on the right, select "as Function" from the drop down. Enter a Function Name.
// 3. Function App usually take 10-15 minutes to activate. You can then use Function Alias for other queries
//
// REFERENCES: 
// Using functions in Azure monitor log queries: https://docs.microsoft.com/azure/azure-monitor/log-query/functions
// Infoblox NIOS logging formats: https://docs.infoblox.com/display/NAG8/Using+a+Syslog+Server
//
// LOG SAMPLES:
// This parser assumes the raw log are formatted as follows:
// 
//      May 13 12:05:52 10.0.0.0 dhcpd[30174]: DHCPDISCOVER from 0a:0b:0c:0d::0f via eth2 TransID 5daf9374: network 10.0.0.0/24: no free leases
//      May 13 12:05:52 10.1.1.1 named[11325]: zone voip.abc.com/IN: ZRQ applied transaction 0101010 with SOA serial 9191919. Zone version is now 0202020
let datasource = (_GetWatchlist('Sources_by_SourceType')| where SearchKey == 'InfobloxNIOS' | project Source);
Syslog
   | where Computer in (datasource)
   | where ProcessName == "dhcpd" and SyslogMessage has "DHCPRELEASE"
   | extend dhcprelease = extract_all(@"of\s(\S+)\sfrom\s((\S+)(\s\(\S+\))?)\svia\s(\S+)(\s\((\w+)\))?(\sTransID\s([a-z0-9]+)\s)?", dynamic([1,2,3,4,5,6,7,8,9,10,11,12]), SyslogMessage)[0]
    | extend IPAddress = tostring(dhcprelease[0]), 
        Client_MAC_Address = tostring(dhcprelease[2]),
        Client_Hostname = extract(@'\((\S+)\)',1,tostring(dhcprelease[3])),
        Relay = tostring(dhcprelease[4]), 
        TransID = tostring(dhcprelease[8]),
        State = tostring(dhcprelease[6]),
        Log_Type= "DHCPRELEASE"
    | project-away SyslogMessage, dhcprelease;