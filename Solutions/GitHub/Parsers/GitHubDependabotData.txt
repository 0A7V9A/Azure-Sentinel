// GitHub Enterprise repository vulnerability Data Parser
// Last Updated Date: June 15, 2022
//
//This parser parses GitHub Enterprise repository vulnerability Data alerts and extract the information from their various components. It is assumed that you are using officially supported Github webhook connector (installed from Content hub)
//
// Parser Notes:
// 1. This parser pulls the logs those are collected into a custom log table entitled githubscanaudit_CL 


githubscanaudit_CL 
|where action_s in ('create', 'dismiss' ,'resolve')
|extend EventType='dependabotAlert-repositoryvulnerability'
|extend alert = todynamic(
replace_string(replace_string(alert_s,"'","\""), "None","\"None\"")),
 repository = todynamic(
replace_string(replace_string(replace_string(replace_string(repository_s,"'","\""), "None","\"None\""),"False","\"False\""),"True","\"True\"")
),
sender = todynamic(
replace_string(replace_string(replace_string(replace_string(sender_s,"'","\""), "None","\"None\""),"False","\"False\""),"True","\"True\"")
) 
|extend alertcreatedate = alert.created_at, alertaffectedrange = alert.affected_range,        
       alertghsaid = alert.ghsa_id,
        alertseverity = alert.severity,
        repositoryfullname = repository.full_name,
        repositoryOwnerlogin = repository.owner.login,
        repositoryurl = repository.url,       
        senderlogin = sender.login,
        sendertype = sender.type
|where alertcreatedate <> '' 
|project TimeGenerated, EventType, alertghsaid, action_s, alertcreatedate, repositoryfullname,repositoryOwnerlogin, repositoryurl, senderlogin, sendertype 



