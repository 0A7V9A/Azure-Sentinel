// GitHub Enterprise code scan Data Parser
// Last Updated Date: June 15, 2022
//
//This parser parses GitHub Enterprise code scan alerts and extract the infromation from their various components. It is assumed that you are using officially supported Github webhook connector (installed from Content hub)
//
// Parser Notes:
// 1. This parser pulls the logs those are collected into a custom log table entitled githubscanaudit_CL 

githubscanaudit_CL 
|order by TimeGenerated desc
|where action_s in ('created', 'reopened_by_user', 'closed_by_user', 'fixed', 'appeared_in_branch', 'reopened')
|extend EventType='CodeScanningAlert'
|extend alert = todynamic(alert_s),
 organization = todynamic(organization_s),
 repository = todynamic(repository_s),
sender = todynamic(sender_s) 
|extend alertcreatedate = alert.created_at, url = alert.url,
        alertdescription = alert.rule.description,
        toolname = alert.tool.name,
        repositoryfullname = repository.full_name,
        repositoryOwnerlogin = repository.owner.login,
        repositoryurl = repository.url,
        orglogin = organization.login,
        orgurl = organization.url,
        senderlogin = sender.login,
        sendertype = sender.type
|where alertcreatedate <> '' 
|project TimeGenerated, EventType, action_s, alertdescription, alertcreatedate, toolname,repositoryfullname,repositoryOwnerlogin, repositoryurl, orglogin, orgurl, senderlogin, sendertype 



