// Usage Instructions:
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name as ibossUrlEvent and specifying Legacy Category.
// This function maps iboss URL events to Azure Sentinel Information Model (ASIM) (https://docs.microsoft.com/azure/sentinel/normalization).
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. ibossUrlEvent | take 10).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
CommonSecurityLog
| where DeviceVendor =~ "iboss" and FlexString2 =~ "URL"
| extend packed = pack(DeviceCustomNumber1Label, DeviceCustomNumber1
                     , DeviceCustomNumber2Label, DeviceCustomNumber2
                     , DeviceCustomNumber3Label, DeviceCustomNumber3
                     , DeviceCustomString1Label, DeviceCustomString1
                     , DeviceCustomString2Label, DeviceCustomString2
                     , DeviceCustomString3Label, DeviceCustomString3
                     , DeviceCustomString4Label, DeviceCustomString4
                     , DeviceCustomString5Label, DeviceCustomString5
                     , DeviceCustomString6Label, DeviceCustomString6
                     , DeviceCustomDate1Label, DeviceCustomDate1
                     , DeviceCustomDate2Label, DeviceCustomDate2)
| evaluate bag_unpack(packed)
| project-rename EventVendor=DeviceVendor
                , EventProduct=DeviceProduct
                , EventProductVersion=DeviceVersion
                , EventResult=DeviceEventClassID
                , EventResultDetails=FlexNumber1
                , DvcAction=DeviceAction
                , RuleName=FlexString1
                , SrcPortNumber=SourcePort
                , SrcIpAddr=SourceIP
                , SrcMacAddr=SourceMACAddress
                , SrcUsername=SourceUserName
                , SrcBytes=SentBytes
                , DstPortNumber=DestinationPort
                , DstIpAddr=DestinationIP
                , DstBytes=ReceivedBytes
                , Domain=DestinationHostName
                , Url=RequestURL
                , UrlCategory=DeviceCustomString2
                , HttpRequestMethod=RequestMethod
                , HttpUserAgent=RequestClientApplication
                , FileSHA256=DeviceCustomString3
                , ThreatName=DeviceCustomString1
                , MalwareDetected=DeviceCustomNumber1
                , CNCDetected=DeviceCustomNumber2
| extend NetworkBytes=SrcBytes+DstBytes
        , EventTime=todatetime(DeviceCustomDate1)
| project-away DeviceCustomNumber1Label
            , DeviceCustomNumber2Label
            , DeviceCustomNumber3
            , DeviceCustomNumber3Label
            , DeviceCustomString1Label
            , DeviceCustomString2Label
            , DeviceCustomString3Label
            , DeviceCustomString4
            , DeviceCustomString4Label
            , DeviceCustomString5
            , DeviceCustomString5Label
            , DeviceCustomString6
            , DeviceCustomString6Label
            , DeviceCustomDate1
            , DeviceCustomDate1Label
            , FlexString1Label
            , FlexString2
            , FlexString2Label
            , FlexNumber1Label
